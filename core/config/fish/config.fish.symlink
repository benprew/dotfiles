# Timing utilities for profiling startup
set -g __config_start (date +%s%N)
function __time_marker
    set -l now (date +%s%N)
    set -l elapsed (math "($now - $__config_start) / 1000000")
    echo "[+$elapsed ms] $argv" >&2
end

function add_path
    set -l new_path $argv[1]
    if test -d $new_path
        # Don't use $fish_user_path variable, it will create duplicates PATH entries
        # https://stackoverflow.com/questions/26208231/modifying-path-with-fish-shell
        #
        # push path to front if it already exists
        fish_add_path --move $new_path
    end
end

function fish_prompt
    # c0 to c4 progress from dark to bright
    # ce is the error colour
    set -g c0 (set_color 005284)
    set -g c1 (set_color 0075cd)
    set -g c2 (set_color 009eff)
    set -g c3 (set_color 6dc7ff)
    set -g c4 (set_color ffffff)
    set -g ce (set_color $fish_color_error)

    set -U fish_prompt_pwd_dir_length 0
    printf "$c3"
    echo -n (prompt_pwd)

    # Use fish's built-in optimized git prompt
    set -l git_info (fish_git_prompt)
    if test -n "$git_info"
        printf " $c4($c2%s$c4)" (string trim -c '() ' $git_info)
    end

    printf "\n$c4><> "
end

set -x EDITOR emacsclient

__time_marker "Starting module loading"

for module in (cat ~/.modules)
    __time_marker "Loading module: $module"
    set module_init_path $HOME/dotfiles/$module/init.fish
    if test -e $module_init_path
        . $module_init_path
    end

    if test -e $HOME/dotfiles/$module/bin
        fish_add_path $HOME/dotfiles/$module/bin
    end
end

__time_marker "Finished module loading"

fish_add_path "/usr/local/bin"
fish_add_path --append "/usr/local/sbin" "$HOME/bin"

__time_marker "Config complete"
