alias e 'emacsclient -n'
alias gs 'git status'
alias gd 'git diff'
alias gco 'git checkout'

for module in (cat ~/.modules)
    set module_init_path $HOME/src/dotfiles/$module/init.fish
    if test -e $module_init_path
        . $module_init_path
    end

    if test -e $HOME/dotfiles/$module/bin
        set -x PATH $PATH $HOME/dotfiles/$module/bin
    end
end
set -x PATH $PATH "/usr/local/sbin" 
set -x PATH $PATH (python -m site --user-base)/bin

function git_branch
    set -g git_branch (git rev-parse --abbrev-ref HEAD ^ /dev/null)
    if [ $status -ne 0 ]
        set -ge git_branch
        set -g git_dirty_count 0
    else
        set -g git_dirty_count (git status --porcelain  | wc -l | sed "s/ //g")
    end
end

function fish_prompt
    # c0 to c4 progress from dark to bright
    # ce is the error colour
    set -g c0 (set_color 005284)
    set -g c1 (set_color 0075cd)
    set -g c2 (set_color 009eff)
    set -g c3 (set_color 6dc7ff)
    set -g c4 (set_color ffffff)
    set -g ce (set_color $fish_color_error)

    set -U fish_prompt_pwd_dir_length 0
    printf "$c3"
    echo -n (prompt_pwd)

    git_branch
    if set -q git_branch
        printf " $c4($c2$git_branch$c4)"
    end

    printf "\n$c4><> "
end
